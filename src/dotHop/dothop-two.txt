title DotHopTwo
author Russell Matney

require_player_movement

========
OBJECTS
========

PlayerA
Blue Green White Black
.000.
.010.
01210
33333
.3.3.

PlayerB
Blue Red White Black
.000.
.010.
01210
33333
.3.3.

TrailBlockA
Purple Red
.000.
0...0
0.1.0
0...0
.000.

TrailBlockB
Purple Red
.000.
0...0
0.1.0
0...0
.000.

TrailOld
Gray

PrevA
Green

PrevB
Red

Dot
Green YellowGreen
..0..
.010.
01011
.010.
..1..

Dotted
Green YellowGreen
.....
.100.
.001.
.010.
.....

Goal
Red Orange
..0..
.101.
00100
.101.
..0..

Background
DarkBrown

=======
LEGEND
=======

. = Background
a = PlayerA
b = PlayerB
o = Dot
t = Goal

Player = a or b
pt = Dot or Goal
x = PlayerA and Dotted
y = PlayerB and Dotted

=======
SOUNDS
=======

sfx0 50392900 (platform sound)

================
COLLISIONLAYERS
================

Background
Dot, Dotted, Goal
Player
TrailOld, TrailBlockA, TrailBlockB
PrevA, PrevB

======
RULES
======

(prevent moving toward the last spot. TODO: undo!)
[ > PlayerA | ... | TrailBlockA ] -> [ PlayerA | ... | TrailBlockA ]
([ > PlayerA | ... | TrailBlockA ] -> [ Dot | ... | PlayerA ])
[ > PlayerB | ... | TrailBlockB ] -> [ PlayerB | ... | TrailBlockB ]
([ > PlayerB | ... | TrailBlockB ] -> [ Dot | ... | PlayerB ])

(prevent moving into the other player)
(TODO cover more cases... this is quite hard-coded)
[ > Player | Player ] -> [ Player | Player ]
[ > Player | Dotted | Player ] -> [ Player | Dotted | Player ]
[ > Player | Dotted | Dotted | Player ] -> [ Player | Dotted | Dotted | Player ]
[ > Player | Dotted | Dotted | Dotted | Player ] -> [ Player | Dotted | Dotted | Dotted | Player ]
[ > Player | Dotted | Dotted | Dotted | Dotted | Player ] -> [ Player | Dotted | Dotted | Dotted | Dotted | Player ]
[ > Player | Dotted | Dotted | Dotted | Dotted | Dotted | Player ] -> [ Player | Dotted | Dotted | Dotted | Dotted | Dotted | Player ]
[ > Player | Dotted | Dotted | Dotted | Dotted | Dotted | Dotted | Player ] -> [ Player | Dotted | Dotted | Dotted | Dotted | Dotted | Dotted | Player ]

(prevent moving if we've reached the Goal)
[ > Player Goal ] -> [ Player Goal ]

(move to platform or Goal, leaving last spot)
[ > PlayerA | ... | pt ] -> [ TrailBlockA PrevA | ... | PlayerA pt ] sfx0
[ > PlayerB | ... | pt ] -> [ TrailBlockB PrevB | ... | PlayerB pt ] sfx0

(mark platform used)
[ Player Dot ] -> [ Player Dotted ]

(otherwise! prevent movement)
[ > Player ] -> [ Player ]

(remove previous blocker)
[ PrevA | ... | TrailBlockA ] -> [ PrevA | ... | ]
[ PrevA ] -> [ ]
[ PrevB  | ... | TrailBlockB  ] -> [ PrevB  | ... | ]
[ PrevB  ] -> [ ]

==============
WINCONDITIONS
==============

No Dot
All Player on Goal

=======
LEVELS
=======

message "Back For More, are you? Time for a warm up"

..oo...
.xo.ot.
...oo..


message "Don't get dizzy now!"

o....t
.o..o.
..xo..
.o.o..
o...o.

message "And, reverse!"

o....x
.o..o.
..to..
.o.o..
o...o.

message "A taste of choice"

oo.o.o.
o..o...
.oto.x.
...o.o.

message "No wrong move?"

o.....o..
o...oo..o
x...ot..o
....o.o..

message "Dancing around it"

oo...o.o
o.o....o
.o.o.x.o
o.o..o..
.t.ooo..
o.o.....

message "Something larger"

.o..o.o..
x.o......
oo..ooooo
..oo.....
o...oto.o
...o...o.

message "Similar, but different"

..o.o.o..
x.o.ooooo
..oo.....
o...oto.o
...o...o.
.....oo..


message "Thank you sir, may I have another"

..o....o.
xoo...oo.
...oo....
o.o..to..
..oo.oo..
.o..o....

message "Don't leave me hanging!"

oo...o..o
o.oo.o...
.ox......
.o...o...
...oto...
...oo...o

message "You can't be serious."

o..o....
o..x.oo.
...o..oo
ooo..o..
.ooo.t..
o.o..o.o

message "I recommend the buddy system."

o.....o..
o...oo..o
o...ot..o
....o.o..
ox.o..o.o
.o.o..o.o

